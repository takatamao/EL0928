Shader "CustomRenderTexture/Crack"
{
    Properties
    {
        [Header(Albedo)]
        [MainColor] _BaseColor("Base Color", Color) = (1.0, 1.0, 1.0, 1.0)
        [MainTexture] _BaseMap("Base Map", 2D) = "white" {}

        [Header(Smoothness and Metallic)]
        _Smoothness("Smoothness", Range(0.0, 1.0)) = 0.0
        _Metallic("Metallic", Range(0.0, 1.0)) = 0.0

        [Header(Crack)]
        _CrackProgress("Crack Progression", Range(0.0, 1.0)) = 0.0 //進行具合
        [HDR] _CrackColor("Crack Color", Color) = (0.0, 0.0, 0.0, 1.0) //ひびの色
        _CrackDetailedness("Detailedness", Range(0.0, 10.0)) = 3.0 //細かさ
        _CrackDepth("CrackDepth", Range(0.0, 1.0)) = 0.5 //ひびの深さ
        _CrackWidth("CrackWidth", Range(0.01, 0.1)) = 0.05 //ひびの幅
        _CrackWallWidth("WallWidth", Range(0.001, 0.2)) = 0.08 //ひび壁の幅

        [Space]

        _RandomSeed("CrackSeed", Int) = 0
    }
     SubShader
     {
        Tags {
            "RenderType" = "Opaque"
            "RenderPipeline" = "UniversalPipeline"
            "UniversalMaterialType" = "Lit"
        }
        LOD 300

        HLSLINCLUDE
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
        ENDHLSL

        Pass
        {
           Name "Crack"
           Tags { "LightMode" = "UniversalForward" }
          
           HLSLPROGRAM
          
           //マテリアル キーワード
           #pragma shader_feature_local_fragment _ALPHATEST_ON //アルファテスト有効
           #pragma shader_feature_local_fragment _ALPHAPREMULTIPLY_ON //プリマルチプライドアルファ有効
           #pragma shader_feature_local_fragment _SPECULARHIGHLIGHTS_OFF //スペキュラーハイライト無効
           #pragma shader_feature_local_fragment _ENVIRONMENTREFLECTIONS_OFF //環境マッピング(反射)無効
           #pragma shader_feature_local_fragment _SPECULAR_SETUP //スペキュラーシェーダーパス有効
           #pragma shader_feature_local _RECEIVE_SHADOWS_OFF //自身に対しての影描画無効
          
           // ユニバーサル パイプライン キーワード
           #pragma multi_compile _ _MAIN_LIGHT_SHADOWS
           #pragma multi_compile _ _MAIN_LIGHT_SHADOWS_CASCADE
           #pragma multi_compile _ _ADDITIONAL_LIGHTS_VERTEX _ADDITIONAL_LIGHTS
           #pragma multi_compile_fragment _ _ADDITIONAL_LIGHT_SHADOWS
           #pragma multi_compile_fragment _ _SHADOWS_SOFT
          
           // GPUインスタンシング
           #pragma multi_compile_instancing
          
          
           #include "Packages/com.unity.render-pipelines.universal/Shaders/LitInput.hlsl"
           #include "Packages/com.unity.render-pipelines.universal/Shaders/LitForwardPass.hlsl"
          
           #pragma vertex Vert
           #pragma fragment Frag
          
           //====変数宣言====//
           float _CrackProgress;
           half4 _CrackColor;
           float _CrackDetailedness;
           float _CrackDepth;
           float _CrackWidth;
           float _CrackWallWidth;
           uint _RandomSeed;
          
           //====構造体====//
           struct v2f {//フラグメントデータ構造体
               float4 positionCS : SV_POSITION;
               float2 uv : TEXCOORD0;
               float3 normalOS: NORMAL;
               float3 normalWS: TEXCOORD1;
               float3 positionOS: TEXCOORD2;
               float3 positionWS: TEXCOORD3;
               float3 viewDirWS: TEXCOORD4;
           };
          
           struct v2g {//ジオメトリデータ構造体


           };

           //====ランダム地点算出メソッド====//
          
           //Xorshift32を用いた疑似乱数関数
           uint XOrShift32(uint value) {
               value = value ^ (value << 13);
               value = value ^ (value >> 17);
               value = value ^ (value << 5);
               return value;
           }
          
           //整数の値を1未満の小数にマッピングする
           float MapToFloat(uint value) {
               const float precion = 100000000.0;
               return (value % precion) * rcp(precion);//valueとprecionの剰余算 * 1/precionの近似値
           }
          
           //3次元のランダムな値を算出する
           float3 Random3(uint3 src, int seed) {
               uint3 random;
               random.x = XOrShift32(mad(src.x, src.y, src.z));
               random.y = XOrShift32(mad(random.x, src.z, src.x) + seed);
               random.z = XOrShift32(mad(random.y, src.x, src.y) + seed);
               random.x = XOrShift32(mad(random.z, src.y, src.z) + seed);
          
               return float3(MapToFloat(random.x), MapToFloat(random.y), MapToFloat(random.z));
           }
           
           //指定した座標に対して、ボロノイパターンの最も近いランダム点と、2番目に近いランダム点を取得する
           void CreateVoronoi(float3 pos, out float3 closest, out float3 secondClosest, out float secondDistance) {
               // セル番号が負の値とならないようにオフセット加算
               const uint offset = 100;
               uint3 cellIdx;
               float3 reminders = modf(pos + offset, cellIdx);
          
               // 対象地点が所属するセルと隣接するセル全てに対してランダム点との距離をチェックし
               // 1番近い点と2番目に近い点を見付ける
               float2 closestDistances = 8.0;
          
               [unroll]
               for (int i = -1; i <= 1; i++)
                   [unroll]
               for (int j = -1; j <= 1; j++)
                   [unroll]
               for (int k = -1; k <= 1; k++) {
                   int3 neighborIdx = int3(i, j, k);
          
                   // そのセル内でのランダム点の相対位置を取得
                   float3 randomPos = Random3(cellIdx + neighborIdx, _RandomSeed);
                   // 対象地点からランダム点に向かうベクトル
                   float3 vec = randomPos + float3(neighborIdx)-reminders;
                   // 距離は全て二乗で比較
                   float distance = dot(vec, vec);
          
                   if (distance < closestDistances.x) {
                       closestDistances.y = closestDistances.x;
                       closestDistances.x = distance;
                       secondClosest = closest;
                       closest = vec;
                   }
                   else if (distance < closestDistances.y) {
                       closestDistances.y = distance;
                       secondClosest = vec;
                   }
               }
          
               secondDistance = closestDistances.y;
           }
          
           //指定した座標がボロノイ図の境界線となるかどうかを0〜1で返す
           float GetVoronoiBorder(float3 pos, out float secondDistance) {
               float3 a, b;
               CreateVoronoi(pos, a, b, secondDistance);
          
               //以下のベクトルの内積が境界線までの距離となる
               //対象地点から、1番近いランダム点と2番目に近い点の中点に向かうベクトル
               //1番近い点と2番目に近い点を結ぶ線の単位ベクトル
               float distance = dot(0.5 * (a + b), normalize(b - a));
          
               return 1.0 - smoothstep(_CrackWidth, _CrackWidth + _CrackWallWidth, distance);
           }
          
           //指定した座標のひび度合いを0〜1で返す
           float GetCrackLevel(float3 pos) {
               // ボロノイ図の境界線で擬似的なクラック模様を表現
               float secondDistance;
               float level = GetVoronoiBorder(pos * _CrackDetailedness, secondDistance);
          
               // 部分的にひびを消すためにノイズを追加
               // 計算量が少なくて済むようにボロノイのF2(2番目に近い点との距離)を利用する
               // 距離が一定値以下の場合はクラック対象から外す
               float f2Factor = 1.0 - sin(_CrackProgress * PI * 0.5);
               float minTh = (2.9 * f2Factor);
               float maxTh = (3.5 * f2Factor);
               float factor = smoothstep(minTh, maxTh, secondDistance * 2.0);
               level *= factor;
          
               return level;
           }
          
           //ひびが入った後の座標を計算
           float3 CalcCrackedPos(float3 localPos, float3 localNormal, out float crackLevel) {
               // ひび対象の場合は法線と逆方向に凹ませる
               crackLevel = GetCrackLevel(localPos);
               float depth = crackLevel * _CrackDepth;
               localPos -= localNormal * depth;
          
               return localPos;
           }
          
           
           //CrackLevelに応じたOcclusionを算出する
           half CalcOcclusion(float crackLevel) {
               // ひびの深さに応じて影を濃くする
               half occlusion = pow(lerp(1.0, 0.9, crackLevel), 2.0);
               // ひびが深い部分で、隣接ピクセルの高低差が大きい場合は影を濃くする
               occlusion *= (crackLevel > 0.95 ? lerp(0.9, 1.0, 1.0 - smoothstep(0.0, 0.1, max(abs(ddy(crackLevel)), abs(ddx(crackLevel))))) : 1.0);
          
               return occlusion;
           }
          
          
           //バーテックスシェーダー
           v2f Vert(Attributes input) {
               v2f output;
               output.positionOS = input.positionOS.xyz;
               output.normalOS = input.normalOS;
          
               Varyings varyings = LitPassVertex(input);
               output.positionCS = varyings.positionCS;
               output.uv = varyings.uv;
               output.positionWS = varyings.positionWS;
               output.normalWS = varyings.normalWS;
          
               return output;
           }
          
           //フラグメントシェーダー
           half4 Frag(v2f input) : SV_Target{
               float crackLevel = 0.0;

                if (_CrackProgress == 0 || dot(input.normalWS, GetViewForwardDir()) > 0.5) {
                    input.positionOS = input.positionOS;
                }
                else {
                    input.positionOS = CalcCrackedPos(input.positionOS, input.normalOS, crackLevel);
                }
          
               Varyings varyings = (Varyings)0;
               varyings.uv = input.uv;
               varyings.positionWS = crackLevel > 0.0 ? TransformObjectToWorld(input.positionOS) : input.positionWS;
               varyings.positionCS = crackLevel > 0.0 ? TransformObjectToHClip(input.positionOS) : input.positionCS;
          
               // 隣接のピクセルとのワールド座標の差分を取得後に外積を求めて法線算出
               varyings.normalWS = crackLevel > 0.0 ? normalize(cross(ddy(varyings.positionWS), ddx(varyings.positionWS))) : input.normalWS;
          
               SurfaceData surfaceData;
               InitializeStandardLitSurfaceData(varyings.uv, surfaceData);
          
               OUTPUT_SH(varyings.normalWS, varyings.vertexSH);
          
               InputData inputData;
               InitializeInputData(varyings, surfaceData.normalTS, inputData);
               inputData.vertexLighting = VertexLighting(varyings.positionWS, inputData.normalWS);
          
          
               /* ひび模様 */
               // ひび対象の場合はクラックカラーを追加
               surfaceData.albedo = lerp(surfaceData.albedo, _CrackColor.rgb, crackLevel);
          
               // ひび部分はAO設定
               surfaceData.occlusion = min(surfaceData.occlusion, CalcOcclusion(crackLevel));
          
               half4 color = UniversalFragmentPBR(inputData, surfaceData);
          
               return color;
           }
           ENDHLSL
        }
     }

     FallBack "Universal Render Pipeline/Lit"
}
